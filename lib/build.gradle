plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace "org.lunapark.antares"
    compileSdk 35

    defaultConfig {
        minSdk 23
        //noinspection OldTargetApi
        targetSdk 35
//        versionCode 1
        versionName "1.0.1"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation("androidx.activity:activity:1.10.1")
    implementation("com.google.code.gson:gson:2.11.0")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Используем артефакты напрямую
                artifact("$buildDir/outputs/aar/${project.name}-release.aar")

                groupId = "com.github.luna-park"
                artifactId = "antares"
                version = android.defaultConfig.versionName

                // Добавляем зависимости в POM
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.implementation.getAllDependencies().each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
    }
}